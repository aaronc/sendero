module senderoxc.util.ImportPrinter;

import senderoxc.Compiler;

import tango.util.Arguments;

class ImportPrinter
{
	this(SenderoXCompiler mod)
	{
		this.rootMod = mod;
	}
	
	char[][char[]] printedMods;
	SenderoXCompiler rootMod;
	
	void print(void delegate(char[]) pr)
	{
		pr("\n\n");
		pr(`digraph d{`);
		pr("\n\t");
		pr(`node [fontname=Helvetica,style=filled];`);
		pr("\n\t");
        pr(`graph [fontname=Helvetica,label="generated by senderoxc"];`);
        pr("\n");
        printMod(rootMod, pr);
        pr(`}`);
	}
	
	void printMod(SenderoXCompiler mod, void delegate(char[]) pr)
	{
		foreach(i; mod.imports)
		{
			pr(`"`);
			pr(mod.modname);
			pr(`" -> "`);
			pr(i.modname);
			pr(`";`);
			pr("\n");
		}
		
		printedMods[mod.modname] = mod.modname;
		
		foreach(i; mod.imports)
		{
			auto pI = i.modname in printedMods;
			if(pI) continue;
			printMod(i, pr);
		}
	}
}

version(SenderoXCImportPrinter)
{
	import senderoxc.Config;
	import sendero.core.Config;
	import tango.io.Stdout;
	import tango.io.File;
	
	int main(char[][] cmdLn)
	{
		if(cmdLn.length > 2) {
			SenderoXCompiler compiler;
			if(cmdLn[1] == "config") {
				SenderoConfig.load(cmdLn[2]);
				SenderoXCConfig.load(cmdLn[2]);
				compiler = SenderoXCompiler.create(SenderoXCConfig().modname);
			}
			else {
				auto args = new Arguments;
				args.define("I").callback((char[] n, char[] p) {
					SenderoXCConfig().includeDirs ~= p;
				});
				args.parse(cmdLn[1..$]);
				compiler = SenderoXCompiler.create(cmdLn[2]);
			}
			auto ip = new ImportPrinter(compiler);
			char[] res;
			ip.print((char[] val){res ~= val;});
			Stdout(res);
			auto f = new File("senderoimp.dump");
			f.write(res);
		}
		else {
			Stdout.formatln("Sendero Import Printer").newline;
			Stdout.formatln("Usage: senderoimp <module name>");
			Stdout.formatln("Example: senderoimp sendero.core.Main");
			Stdout.formatln("Ouput is printed to stdout and saved to a file"
					" called senderoimp.dump in graphviz format.");
		}
		return 0;
	}
}